<Inicial> ::= <ListaClases> "eof"

<ListaClases> ::= <Clase> <ListaClasesAUX> | <Interfaz> <ListaClasesAUX>
<ListaClasesAUX> ::= <ListaClases> |

<Clase> ::= "class" "idClase" <Genericidad> <Herencia> <Implementa> "{" <ListaMiembros> "}"
<Interfaz> ::= "interface" "idClase" <Genericidad> <HerenciaInterfaz> "{" <ListaMiembrosInterfaz> "}"

<Genericidad> ::= "<" "idClase" ">" |

<Herencia> ::= "extends" "idClase" <Genericidad> |
<Implementa> ::= "implements" <Genericidad> <ListaInterfaces> |
<ListaInterfaces> ::= "idClase" <Genericidad> <ListaInterfacesAux>
<ListaInterfacesAux> ::= "," <ListaInterfaces> |

<HerenciaInterfaz> ::= "extends"  <ListaInterfaces> |

<ListaMiembros> ::= <Miembro> <ListaMiembros> |
<ListaMiembrosInterfaz> ::= <MetodoDeclaracion> <ListaMiembrosInterfaz> |

<Miembro> ::= <AttrVisibilidad>  | <MetodoConCuerpo>
<AttrVisibilidad> ::= "public" <Attr> | "protected" <Attr> | "private" <Attr> | <AttrOCons>

<Attr> ::= <EstaticoOVacio> <Tipo> <AsignacionAttr> ";"
<EstaticoOVacio> ::= "static" |

<AttrOCons> ::= "idClase" <Genericidad> <ConstructorOAttr> | <TipoPrimitivo> <AsignacionAttr> ";"
<ConstructorOAttr> ::= <Constructor> | <AsignacionAttr> ";"

<Constructor> ::= <ArgsFormales> <Bloque>

<AsignacionAttr> ::= "idMetVar" <AsignacionAttrAux>
<AsignacionAttrAux> ::= "=" <Expresion> <ListaAsignacion> | <ListaAsignacion>
<ListaAsignacion> ::= "," <AsignacionAttr> |

<CabeceraMetodo> ::= <FormaMetodo> <TipoMetodo> "idMetVar" <ArgsFormales>
<MetodoConCuerpo> ::= <CabeceraMetodo> <Bloque>
<MetodoDeclaracion> ::= <CabeceraMetodo> ";"

<Tipo> ::= <TipoPrimitivo> | "idClase" <Genericidad>
<TipoPrimitivo> ::= "boolean" | "char" | "int" | "String"

<FormaMetodo> ::= "static" | dynamic

<TipoMetodo> ::= <Tipo> | "void"

<ArgsFormales> ::= "(" <ListaArgsFormalesOVacio> ")"

<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> |

<ListaArgsFormales> ::= <ArgFormal> <ListaArgsFormalesAUX>
<ListaArgsFormalesAUX> ::= "," <ListaArgsFormales> |

<ArgFormal> ::= <Tipo> "idMetVar"

<Bloque> ::= "{" <ListaSentencias> "}"

<ListaSentencias> ::= <Sentencia> <ListaSentencias> |

<Sentencia> ::= ";"| <Acceso> <SentenciaAUX> ";" | "static" "idClase" <PreAccesoEstatico> ";"  |
 <TipoPrimitivo> <AsignacionAttr> ";" |
 "idClase" <Genericidad> <AccesoEstaticoODeclaracion> ";" |
 "if" ( <Expresion> ) <Sentencia> <SentenciaAUX1> |
 "while" ( <Expresion> ) <Sentencia> | <Bloque> | "return" <ExpresionOVacio> ";"

<SentenciaAUX> ::= <Asignacion> |
<SentenciaAUX1> ::= "else" <Sentencia> |


<AccesoEstaticoODeclaracion> ::= <AsignacionAttr> | <PreAccesoEstatico>

<Asignacion> ::= <TipoDeAsignacion> <Expresion>
<TipoDeAsignacion> ::= "=" | "+=" | "-="

<ExpresionOVacio> ::= <Expresion> |

<Expresion> ::= <Or>

<Or> ::= <And> <OrAux>
<OrAux> ::=  <Op1> <And> <OrAux> |

<And> ::= <EqualsExp> <AndAux>
<AndAux> ::=  <Op2> <EqualsExp> <AndAux> |

<EqualsExp> ::= <InEq> <EqualsAux>
<EqualsAux> ::=  <Op3> <InEq> <EqualsAux> |

<InEq> ::= <Add> <InEqAux>
<InEqAux> ::=  <Op4> <Add> <InEqAux> |

<Add> ::= <Mult> <AddAux>
<AddAux> ::=  <Op5> <Mult> <AddAux> |

<Mult> ::= <ExpresionUnaria> <MultAux>
<MultAux> ::=  <Op6> <ExpresionUnaria> <MultAux> |

<Op1> ::= "||"
<Op2> ::= "&&"
<Op3> ::= "==" | "!="
<Op4> ::= "<" | ">" | "<=" | ">="
<Op5> ::= "+" | "-"
<Op6> ::= "*" | "/" | "%"

<ExpresionUnaria> ::= <OperadorUnario> <Operando> | <Operando>

<OperadorUnario> ::= "+" | "-" | "!"

<Operando> ::= <Literal> | <AccesoOperando>

<Literal> ::= "null" | "true" | "false" | "intLiteral" | "charLiteral" | "stringLiteral"

<PreAccesoEstatico> ::= <VarOMetodoEncadenado> <Encadenado> <SentenciaAUX>
<Acceso> ::= <Primario> <Encadenado>
<AccesoOperando> ::= <Acceso> | <AccesoEstatico> <Encadenado>

<Primario> ::= <AccesoThis> | <AccesoConstructor> |<AccesoVarOMetodo> | "(" <Expresion> ")"

<AccesoThis> ::= "this"

<AccesoVarOMetodo> ::= "idMetVar" <AccesoVarOMetodoAUX>
<AccesoVarOMetodoAUX> ::= <ArgsActuales> |
<AccesoEstatico> ::= "static" "idClase" <VarOMetodoEncadenado> <Encadenado> | "idClase" <VarOMetodoEncadenado> <Encadenado>

<AccesoConstructor> ::= "new" "idClase" <Genericidad> <ArgsActuales>

<ArgsActuales> ::= "(" <ListaExpsOVacio> ")"

<ListaExpsOVacio> ::= <ListaExps> |

<ListaExps> ::= <Expresion> <ListaExpsAUX>
<ListaExpsAUX> ::= "," <ListaExps> |

<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> |

<VarOMetodoEncadenado> ::= "." "idMetVar" <VarOMetodoEncadenadoAUX>
<VarOMetodoEncadenadoAUX> ::= <ArgsActuales> |